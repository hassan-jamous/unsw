group 'unsw'

apply plugin: 'checkstyle'
apply plugin: 'org.springframework.boot'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.10.RELEASE")
    }
}

repositories {
    mavenCentral()
}

ext {
    springVersion = "4.3.14.RELEASE"
    lombokVersion = "1.16.16"
    jacksonVersion = "2.9.4"
    junitVersion = "4.12"
    mockitoVersion = "2.15.0"
    checkStyleVersion = "7.0"
    servletApiVersion = "3.1.0"
    jspApiVersion = "2.3.1"
    jstlVersion = "1.2"
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.apache.tomcat.embed:tomcat-embed-jasper"
    compile 'org.springframework.boot:spring-boot-starter-tomcat'
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "com.fasterxml.jackson.core:jackson-databind"
    compile "org.projectlombok:lombok:${lombokVersion}"
    compile "javax.servlet:javax.servlet-api:${servletApiVersion}"
    compile "javax.servlet.jsp:javax.servlet.jsp-api:${jspApiVersion}"
    compile "javax.servlet:jstl:${jstlVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    integrationTestCompile "org.springframework:spring-test:${springVersion}"
}

checkstyle {
    toolVersion = "7.0"
}
checkstyleMain {
    configFile = rootProject.file('buildsupport/checkstyle/checkstyle-main.xml')
}
checkstyleTest {
    configFile = rootProject.file('buildsupport/checkstyle/checkstyle-test.xml')
}
checkstyleIntegrationTest {
    configFile = rootProject.file('buildsupport/checkstyle/checkstyle-test.xml')
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test