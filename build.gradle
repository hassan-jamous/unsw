group 'unsw'


apply plugin: 'war'
apply plugin: 'checkstyle'
apply plugin:'application'
mainClassName = "application.SpringApplication"

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile 'org.springframework:spring-context:4.3.14.RELEASE'
    compile 'org.springframework:spring-web:4.3.14.RELEASE'
    compile 'org.springframework:spring-webmvc:4.3.14.RELEASE'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.4'
    compile 'org.projectlombok:lombok:1.16.16'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.mockito:mockito-core:1.+"
    integrationTestCompile group: 'org.springframework', name: 'spring-test', version: '4.3.14.RELEASE'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'javax.servlet.jsp', name: 'javax.servlet.jsp-api', version: '2.3.1'
    compile group: 'javax.servlet', name: 'jstl', version: '1.2'

}

checkstyle {
    toolVersion = "7.0"
}
checkstyleMain {
    configFile = rootProject.file('buildsupport/checkstyle/checkstyle-main.xml')
}
checkstyleTest {
    configFile = rootProject.file('buildsupport/checkstyle/checkstyle-test.xml')
}
checkstyleIntegrationTest {
    configFile = rootProject.file('buildsupport/checkstyle/checkstyle-test.xml')
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test